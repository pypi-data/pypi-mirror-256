// ********************************************************************************
// POU Declaration List
// ********************************************************************************
?start:
    | ("import" NAME ".*;")* type pou "("")" "{" (vdcl | statlist)* "}" -> module
    | ("import" NAME ".*;")* type pou "(" idcl ")" "{" (vdcl | statlist)* "}" -> module
pou: NAME -> name

type: TYPE
TYPE: "module"


// ********************************************************************************
// Interface/Variable Declaration List
// ********************************************************************************
idcl: (var_input | var_output | var_in_out) ("," var_input | "," var_output | "," var_in_out)* -> idcl

vdcl: var_local -> vdcl

dcllist: declaration

declaration:
    | variable ";"

var_input: datatype "?" variable

var_output: datatype "!" variable

var_in_out: datatype variable

var_local:
    | datatype dcllist
    | array datatype dcllist


// ********************************************************************************
// Datatypes
// ********************************************************************************
datatype: TYPE_BOOL | TYPE_INT | TYPE_REAL | TYPE_TIME | macro_name

TYPE_BOOL: "bool"
TYPE_INT: "int"
TYPE_REAL: "real"
TYPE_TIME: "nat"
array: "[" constant_int "]"
macro_name: MACRO_NAME


// ********************************************************************************
// Functions/User-POUs
// ********************************************************************************
macro:
    | MACRO_NAME "." macro_out
    | MACRO_NAME ":" NAME "(" _expr ("," _expr)* ");"
macro_out: MACRO_NAME


// ********************************************************************************
// Arithmetic operators
// ********************************************************************************
multiply_with: _expr "*" _expr
divide_by: _expr "/" _expr
modulo: _expr "%" _expr
adding: _expr "+" _expr
subtracting: _expr "-" _expr
expt: "exp(" _expr "," _expr ")"
unaryminus: "-" _expr


// ********************************************************************************
// Boolean operators
// ********************************************************************************
not_op: "!(" _expr ")"
and_op: _expr "and" _expr
xor_op: _expr "xor" _expr
or_op: _expr "or" _expr


// ********************************************************************************
// Numeric operators
// ********************************************************************************
less_or_equal.1: _expr "<=" _expr
greater_or_equal.1: _expr ">=" _expr
less_than: _expr "<" _expr
greater_than: _expr ">" _expr


// ********************************************************************************
// Equality
// ********************************************************************************
equality: _expr "==" _expr | "EQ(" _expr "," _expr ")"
inequality.1: _expr "!=" _expr | "NE(" _expr "," _expr ")"

// ********************************************************************************
// General expressions
// ********************************************************************************
_expr:
    | equality
	| variable ("[" constant_int "]")*
	| multiply_with
	| divide_by
	| modulo
	| adding
	| subtracting
	| "(" _expr ")"
    | expt
	| constant
	| not_op
	| and_op
	| xor_op
	| or_op
	| inequality
	| less_or_equal
	| greater_or_equal
	| less_than
	| greater_than
	| false
	| true
    | unaryminus
    | if_statement_short

false: FALSE
FALSE: "false"
true: TRUE
TRUE: "true"


// ********************************************************************************
// Statementlist
// ********************************************************************************
constant.1: UINT | MS_DURATION | REAL
constant_int.1: UINT
UINT: /[0-9]+/
REAL: /[0-9][.]?[0-9]*/
MS_DURATION: "t#"UINT"s"
//NUMBER: /[+-]?[0-9]+/

statlist: statement*

statement:
    | immediate_assignment
    | if_statement
    | while_statement
    | repeat_statement
    | macro
    | pause_statement
    | next_statement

// ********************************************************************************
// Next
// ********************************************************************************
next_statement: "next(" variable("["constant_int"]")* ")" "=" _expr ";"

// ********************************************************************************
// Pause
// ********************************************************************************
pause_statement: PAUSE ";"
PAUSE: "pause"

// ********************************************************************************
// Assignments
// ********************************************************************************
immediate_assignment: variable("["constant_int"]")* "=" _expr ";"


// ********************************************************************************
// selection statement
// ********************************************************************************
if_statement: "if(" _expr "){" statlist "}else{" statlist "}"

if_statement_short: "(" _expr "?" _expr ":" _expr ")"



// ********************************************************************************
// iteration statements
// ********************************************************************************
while_statement: "while(" _expr "){" statlist "pause;" "}"

repeat_statement: "do{" statlist "pause;" "}" "while(!(" _expr "));"


// ********************************************************************************
// Additional Terminals and_op rules
// ********************************************************************************
variable: NAME


// ********************************************************************************
// Lark statements
// ********************************************************************************
MULTI_LINE_COMMENT: /\(\*.*?\*\)/s
SINGLE_LINE_COMMENT: /\s*/ "//" /[^\n]/*

%ignore WS
%ignore MULTI_LINE_COMMENT
%ignore SINGLE_LINE_COMMENT
%import common.WS
NAME: /[_]?(?!(and|or|xor|pause|true|false|int|real\b))[a-zA-Z0-9_]\w*/
MACRO_NAME: /[_]?(?!(and|or|xor|pause|true|false|int|real\b))[a-zA-Z_]\w*/